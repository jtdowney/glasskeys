//// A Gleam library for server-side verification of WebAuthn/FIDO2 credentials.
////
//// glasskeys provides everything you need to implement passwordless authentication
//// using passkeys and security keys. It handles the cryptographic verification
//// of both registration and authentication ceremonies.
////
//// ## Quick Start
////
//// ### Registration (creating a new credential)
////
//// ```gleam
//// import glasskeys
//// import glasskeys/registration
////
//// // 1. Build a challenge
//// let #(challenge_b64, verifier) =
////   registration.new()
////   |> registration.origin("https://example.com")
////   |> registration.rp_id("example.com")
////   |> registration.build()
////
//// // 2. Send challenge_b64 to browser as part of PublicKeyCredentialCreationOptions
//// //    Store verifier in session for step 3
////
//// // 3. Verify the browser's response
//// case registration.verify(
////   attestation_object: attestation_object,
////   client_data_json: client_data_json,
////   challenge: verifier,
//// ) {
////   Ok(credential) -> {
////     // Store credential.id, credential.public_key, credential.sign_count
////   }
////   Error(e) -> // Handle error
//// }
//// ```
////
//// ### Authentication (verifying an existing credential)
////
//// ```gleam
//// import glasskeys
//// import glasskeys/authentication
////
//// // 1. Build a challenge
//// let #(challenge_b64, verifier) =
////   authentication.new()
////   |> authentication.origin("https://example.com")
////   |> authentication.rp_id("example.com")
////   |> authentication.allowed_credentials([stored_credential_id])
////   |> authentication.build()
////
//// // 2. Send challenge_b64 to browser as part of PublicKeyCredentialRequestOptions
//// //    Store verifier in session for step 3
////
//// // 3. Verify the browser's response
//// case authentication.verify(
////   authenticator_data: authenticator_data,
////   client_data_json: client_data_json,
////   signature: signature,
////   credential_id: credential_id,
////   challenge: verifier,
////   stored: stored_credential,
//// ) {
////   Ok(updated_credential) -> {
////     // Update stored sign_count with updated_credential.sign_count
////   }
////   Error(e) -> // Handle error
//// }
//// ```
////
//// ## Supported Features
////
//// - ES256 (P-256 + SHA-256) signatures
//// - "none" attestation format
//// - User verification and user presence policies
//// - Sign count verification for cloned authenticator detection
////

/// Phantom type indicating a builder field has been set.
@internal
pub type Has {
  Has
}

/// Phantom type indicating a builder field is missing.
@internal
pub type Missing {
  Missing
}

/// User verification requirement for the authenticator.
///
/// - `VerificationRequired`: The authenticator must verify the user (e.g., biometric or PIN).
/// - `VerificationPreferred`: Verification is preferred but not required.
/// - `VerificationDiscouraged`: Verification should be skipped if possible.
pub type UserVerification {
  VerificationRequired
  VerificationPreferred
  VerificationDiscouraged
}

/// User presence requirement for the authenticator.
///
/// - `PresenceRequired`: The user must be present (e.g., touch the authenticator).
/// - `PresencePreferred`: Presence is preferred but not required.
/// - `PresenceDiscouraged`: Presence check should be skipped if possible.
pub type UserPresence {
  PresenceRequired
  PresencePreferred
  PresenceDiscouraged
}

/// A unique identifier for a WebAuthn credential, generated by the authenticator.
pub type CredentialId =
  BitArray

/// A public key in uncompressed point format (0x04 || X || Y for EC2 keys).
pub type PublicKey =
  BitArray

/// A verified WebAuthn credential returned after successful registration or authentication.
///
/// Store the `id`, `public_key`, and `sign_count` after registration. Update `sign_count`
/// after each successful authentication to detect cloned authenticators.
///
/// The `user_verified` field indicates whether the authenticator performed user verification
/// (e.g., biometric or PIN) during this ceremony. This is useful when `UserVerification` is
/// set to `Preferred` and you want to know if verification actually occurred.
pub type Credential {
  Credential(
    id: CredentialId,
    public_key: PublicKey,
    sign_count: Int,
    user_verified: Bool,
  )
}

/// Errors that can occur during WebAuthn verification.
pub type GlasskeysError {
  /// A verification field does not match the expected value.
  /// The field parameter indicates what failed: "challenge", "type", "origin", or "rp_id".
  VerificationMismatch(field: String)
  /// The key format, algorithm, or curve is not supported.
  UnsupportedKey(reason: String)
  /// A feature (e.g., extensions) is not supported.
  UnsupportedFeature(reason: String)
  /// Failed to parse data (CBOR, JSON, or authenticator data).
  ParseError(message: String)
  /// The attestation format or statement is invalid.
  InvalidAttestation(reason: String)
  /// The cryptographic signature verification failed.
  InvalidSignature
  /// The credential ID is not in the allowed credentials list.
  CredentialNotAllowed
  /// The sign count decreased, indicating a possible cloned authenticator.
  SignCountRegression
  /// User presence was required but not asserted by the authenticator.
  UserPresenceFailed
  /// User verification was required but not performed by the authenticator.
  UserVerificationFailed
}
